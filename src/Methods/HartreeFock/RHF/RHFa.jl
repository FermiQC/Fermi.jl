function RHF(Alg::RHFa)
    ints = IntegralHelper{Float64}()
    RHF(ints, Alg)
end

function RHF(mol::Molecule, Alg::RHFa)
    RHF(IntegralHelper{Float64}(molecule=mol), Alg)
end

function RHF(ints::IntegralHelper{Float64}, Alg::RHFa)

    Fermi.HartreeFock.hf_header()

    output("Collecting necessary integrals...")
    t = @elapsed begin
        ints["S"]
        ints["T"]
        ints["V"]
        ints["ERI"]
    end
    output("Done in {:10.5f} s", t)

    guess = Options.get("scf_guess")
    if guess == "core"
        C, Œõ = RHF_core_guess(ints)
    elseif guess == "gwh"
        C, Œõ = RHF_gwh_guess(ints)
    end

    RHF(ints, C, Œõ, Alg)
end

function RHF(wfn::RHF, Alg::RHFa)

    Fermi.HartreeFock.hf_header()

    # Projection of A‚Üí B done using equations described in Werner 2004 
    # https://doi.org/10.1080/0026897042000274801

    output("Using {} wave function as initial guess", wfn.orbitals.basis)

    intsB = IntegralHelper{Float64}()

    # Assert both A and B have the same molecule.
    if intsB.molecule != wfn.molecule
        output(" ! Input molecule does not match the molecule from the RHF wave function !")
    end

    basisB = Options.get("basis")

    Sbb = intsB["S"]
    Œõ = Array(Sbb^(-1/2))

    Ca = wfn.orbitals.C
    bsA = Fermi.GaussianBasis.BasisSet(wfn.molecule, wfn.orbitals.basis)
    bsB = Fermi.GaussianBasis.BasisSet(intsB.molecule, intsB.basis)
    Sab = Fermi.Integrals.ao_1e(bsA, bsB, "overlap")

    T = transpose(Ca)*Sab*(Sbb^-1.0)*transpose(Sab)*Ca
    Cb = (Sbb^-1.0)*transpose(Sab)*Ca*T^(-1/2)
    Cb = real.(Cb)

    RHF(intsB, FermiMDArray(Cb), FermiMDArray(Œõ), Alg)
end

function RHF(ints::IntegralHelper{Float64, <:AbstractERI, AtomicOrbitals}, C::FermiMDArray{Float64,2}, Œõ::FermiMDArray{Float64,2}, Alg::RHFa)

    molecule = ints.molecule
    output(Fermi.Geometry.string_repr(molecule))
    # Grab some options
    maxit = Options.get("scf_max_iter")
    Etol  = Options.get("scf_e_conv")
    Dtol  = Options.get("scf_max_rms")
    do_diis = Options.get("diis")
    oda = Options.get("oda")
    oda_cutoff = Options.get("oda_cutoff")
    oda_shutoff = Options.get("oda_shutoff")

    # Variables that will get updated iteration-to-iteration
    ite = 1
    E = 0.0
    ŒîE = 1.0
    Drms = 1.0
    Drms = 1.0
    diis = false
    damp = 0.0 
    converged = false
    
    # Build a diis_manager, if needed
    if do_diis
        DM = Fermi.DIIS.DIISManager{Float64,Float64}(size=Options.get("ndiis"))
        diis_start = Options.get("diis_start")
    end

    # Grab ndocc,nvir
    ndocc = try
        Int((molecule.NŒ± + molecule.NŒ≤)/2)
    catch InexactError
        throw(FermiException("Invalid number of electrons $(molecule.NŒ± + molecule.NŒ≤) for RHF method."))
    end
    nvir = size(C,2) - ndocc
    nao = size(C,1)

    output(" Number of AOs:                        {:5.0d}", nao)
    output(" Number of Doubly Occupied Orbitals:   {:5.0d}", ndocc)
    output(" Number of Virtual Spatial Orbitals:   {:5.0d}", nvir)

    
    S = ints["S"]
    T = ints["T"]
    V = ints["V"]
    ERI = ints["ERI"]

    # Form the density matrix from occupied subset of guess coeffs
    Co = C[:, 1:ndocc]
    @tensor D[u,v] := Co[u,m]*Co[v,m]
    D_old = deepcopy(D)
    eps = FermiMDzeros(Float64,ndocc+nvir)

    # Build the inital Fock Matrix and diagonalize
    F = FermiMDzeros(Float64,nao,nao)
    build_fock!(F, T + V, D, ERI)
    FÃÉ = deepcopy(F)
    DÃÉ = deepcopy(D)
    N = length(D) # Number of elements in D (For RMS computation)
    output(" Guess Energy {:20.14f}", RHFEnergy(D,T+V,F))

    output("\n Iter.   {:>15} {:>10} {:>10} {:>8} {:>8} {:>8}", "E[RHF]", "ŒîE", "D·µ£‚Çò‚Çõ", "t", "DIIS", "damp")
    output(repeat("-",80))
    t = @elapsed while ite ‚â§ maxit
        t_iter = @elapsed begin
            # Produce Ft
            Ft = Œõ'*F*Œõ

            # Get orbital energies and transformed coefficients
            eps,Ct = diagonalize(Ft, hermitian=true)

            # Reverse transformation to get MO coefficients
            C = Œõ*Ct

            # Produce new Density Matrix
            Co = C[:,1:ndocc]
            @tensor D[u,v] = Co[u,m]*Co[v,m]

            # Build the Fock Matrix
            build_fock!(F, T + V, D, ERI)
            Eelec = RHFEnergy(D, T + V, F)

            # Compute Energy
            Enew = Eelec + molecule.Vnuc

            # Store vectors for DIIS
            if do_diis
                err = transpose(Œõ)*(F*D*ints["S"] - ints["S"]*D*F)*Œõ
                push!(DM, F, err)
            end

            # Branch for ODA vs DIIS convergence aids
            diis = false
            damp = 0.0
            # Use ODA damping?
            if oda && Drms > oda_cutoff && ite < oda_shutoff
                diis = false
                dD = D - DÃÉ
                s = tr(FÃÉ * dD)
                c = tr((F - FÃÉ) * (dD))
                if c <= -s/(2*c)
                    Œª = 1.0
                else
                    Œª = -s/(2*c)
                end
                FÃÉ .= (1-Œª)*FÃÉ + Œª*F
                DÃÉ .= (1-Œª)*DÃÉ + Œª*D
                damp = 1-Œª
                F .= FÃÉ
            
            # Or Use DIIS?
            elseif do_diis && ite > diis_start
                diis = true
                F = Fermi.DIIS.extrapolate(DM)
            end

            # Compute the Density RMS
            ŒîD = D - D_old
            Drms = ‚àö(sum(ŒîD.^2) / N)

            # Compute Energy Change
            ŒîE = Enew - E
            E = Enew
            D_old .= D
        end
        output("    {:<3} {:>15.10f} {:>11.3e} {:>11.3e} {:>8.2f} {:>8}    {:5.2f}", ite, E, ŒîE, Drms, t_iter, diis, damp)
        ite += 1

        if (abs(ŒîE) < Etol) & (Drms < Dtol) & (ite > 5)
            converged = true
            break
        end
    end

    output(repeat("-",80))
    output("    RHF done in {:>5.2f}s", t)
    output("    @Final RHF Energy     {:>20.12f} E‚Çï", E)
    output("\n   ‚Ä¢ Orbitals Summary",)
    output("\n {:>10}   {:>15}   {:>10}", "Orbital", "Energy", "Occupancy")
    for i in eachindex(eps)
        output(" {:>10}   {:> 15.10f}   {:>6}", i, eps[i], (i ‚â§ ndocc ? "‚Üø‚áÇ" : ""))
    end
    output("")
    if converged
        output("   ‚úî  SCF Equations converged üòÑ")
    else
        output("‚ùó SCF Equations did not converge in {:>5} iterations ‚ùó", maxit)
    end
    output(repeat("-",80))

    Orbitals = RHFOrbitals(molecule, ints.basis, eps, E, C)

    return RHF(molecule, E, ndocc, nvir, Orbitals, ŒîE, Drms)
end